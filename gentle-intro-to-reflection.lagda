# | ~C-x C-a~ | transform org ~org-agda~ blocks to literate Agda blocs        |
# | ~C-x C-o~ | transform literate Agda code delimiters to org ~org-agda~ src |
#
#+TITLE: A Gentle Introduction to Reflection in Agda
#+DESCRIPTION: How can we use a single proof to prove two different theorems?
#+AUTHOR: Musa Al-hassy
#+EMAIL: alhassy@gmail.com
#+STARTUP: indent

#+CATEGORIES: Agda Org Emacs
#+OPTIONS: html-postamble:nil toc:nil
#+IMAGE: ../assets/img/org_logo.png
#+SOURCE: https://raw.githubusercontent.com/alhassy/org-agda-mode/master/literate.lagda

# INCLUDE: ~/Dropbox/MyUnicodeSymbols.org

* Abstract       :ignore:
#+BEGIN_CENTER
*Abstract*
#+END_CENTER

/One proof for two different theorems!/

Let's learn how we can do that in Agda.

This tutorial is the result of mostly experimenting with the
[[https://agda.readthedocs.io/en/v2.5.2/language/reflection.html][documentation]] on Agda's reflection mechanism, which essentially
only exposes the reflection interface and provides a few tiny examples.

Everything here works with Agda version 2.6.0.

* Imports

#+BEGIN_SRC org-agda
module gentle-intro-to-reflection where

open import Relation.Binary.PropositionalEquality hiding ([_])
open import Reflection hiding (_‚âü_ ; name)
open import Data.List
open import Relation.Nullary

open import Data.Nat
open import Data.Bool
open import Data.String

open import Data.Unit

import Level as Level

open import Data.Product
#+END_SRC

* Intro

/Reflection/ is the ability to convert program code into an abstract syntax,
a data structure that can be manipulated like any other.

Consider, for example, the tedium of writing a decidable equality for an enumerated type.
Besides being tedious and error-prone, the inexpressibility of
what should be a mechanically-derivable concept
obscures the corresponding general principle underlying it, thus foregoing
any machine assistance in ensuring any correctness or safety-ness guarantees.
Reflection allows a more economical and disciplined approach.

It is the aim of this tutorial to show how to get started with reflection in Agda.
To the best of my knowledge there is no up to date tutorial on this matter.


There are three main types in Agda's reflection mechanism:
~Name, Arg, Term~.

* ~NAME~ ‚îÄType of known identifiers                         :forward_todo_link:

~Name~ is the type of quoting identifiers, Agda names.
Elements of this type can be formed and pattern matched using
the ~quote~ keyword.

#+BEGIN_SRC org-agda
a-name : Name
a-name = quote ‚Ñï

isNat : Name ‚Üí Bool
isNat (quote ‚Ñï) = true
isNat _         = false

-- bad : Set ‚Üí Name
-- bad s = quote s  {- s is not known -}
#+END_SRC

+ ~NAME~ comes equipped with equality, ordering, and a show function.
+ Quote will not work on function arguments; the identifier must be known.

~NAME~ essentially provides us with the internal representation of a known name,
for which we can query to obtain its definition or type.
Later we will show how to get the type constructors of ~‚Ñï~ from its name.

* ~Arg~ ‚îÄType of arguments

Arguments in Agda may be hidden or computationally irrelevant.
This information is captured by the ~Arg~ type.

#+BEGIN_EXAMPLE org-agda
{- Arguments can be (visible), {hidden}, or ‚¶Éinstance‚¶Ñ -}
data Visibility : Set where
  visible hidden instance‚Ä≤ : Visibility

{-Arguments can be relevant or irrelevant: -}
data Relevance : Set where
  relevant irrelevant : Relevance

{- Visibility and relevance characterise the behaviour of an argument: -}
data ArgInfo : Set where
  arg-info : (v : Visibility) (r : Relevance) ‚Üí ArgInfo

data Arg (A : Set) : Set where
  arg : (i : ArgInfo) (x : A) ‚Üí Arg A
#+END_EXAMPLE

For example, let's create some helpers that make arguments of any given type ~A~:
#+BEGIN_SRC org-agda
variable {A} : Set

{- ùìãisible ùìáelevant ùí∂rgument -}
ùìãùìáùí∂ : A ‚Üí Arg A
ùìãùìáùí∂ = arg (arg-info visible relevant)

{- ùíΩidden ùìáelevant ùí∂rgument -}
ùíΩùìáùí∂ : A ‚Üí Arg A
ùíΩùìáùí∂ = arg (arg-info hidden relevant)
#+END_SRC

* ~Term~ ‚îÄType of terms

We use the ~quoteTerm~ keyword to turn a well-typed fragment of code
---concrete syntax--- into a value of the ~Term~ datatype ---the abstract syntax.
Here's the definition of ~Term~:
#+BEGIN_EXAMPLE org-agda
data Term where

  {- A variable has a De Bruijn index and may be applied to arguments. -}
  var       : (x : ‚Ñï)  (args : List (Arg Term)) ‚Üí Term

  {- Constructors and definitions may be applied to a list of arguments. -}
  con       : (c : Name) (args : List (Arg Term)) ‚Üí Term
  def       : (f : Name) (args : List (Arg Term)) ‚Üí Term

  {- Œª-abstractions bind one varaible; ‚Äút‚Äù is the string name of the variable
    along with the body of the lambda.
  -}
  lam       : (v : Visibility) (t : Abs Term) ‚Üí Term  {- Abs A  ‚âÖ  String √ó A -}
  pat-lam   : (cs : List Clause) (args : List (Arg Term)) ‚Üí Term

  {- Telescopes, or function types; Œª-abstraction for types. -}
  pi        : (a : Arg Type) (b : Abs Type) ‚Üí Term

  {- ‚ÄúSet n‚Äù or some term that denotes a type -}
  agda-sort : (s : Sort) ‚Üí Term

  {- Metavariables; introduced via quoteTerm -}
  meta      : (x : Meta) ‚Üí List (Arg Term) ‚Üí Term

  {- Literal  ‚âÖ  ‚Ñï | Word64 | Float | Char | String | Name | Meta -}
  lit       : (l : Literal) ‚Üí Term

  {- Items not representable by this AST; e.g., a hole. -}
  unknown   : Term {- Treated as '_' when unquoting. -}

data Sort where
  set     : (t : Term) ‚Üí Sort {- A Set of a given (possibly neutral) level. -}
  lit     : (n : Nat) ‚Üí Sort  {- A Set of a given concrete level. -}
  unknown : Sort

data Clause where
  clause        : (ps : List (Arg Pattern)) (t : Term) ‚Üí Clause
  absurd-clause : (ps : List (Arg Pattern)) ‚Üí Clause
#+END_EXAMPLE



** Example: Simple Terms

The first example below demonstrates that ~true~ is a type ‚Äúcon‚Äùstructor
that takes no arguments, whence the ~[]~.
#+BEGIN_SRC org-agda
_ : quoteTerm true ‚â° con (quote true) []
_ = refl

_ : quoteTerm 1 ‚â° lit (nat 1)
_ = refl

_ :    quoteTerm (suc zero)
     ‚â° con (quote suc) (arg (arg-info visible relevant) (quoteTerm zero) ‚à∑ [])
_ = refl

{- Using our helper ùìãùìáùí∂ -}
_ : quoteTerm (suc zero) ‚â° con (quote suc) (ùìãùìáùí∂ (quoteTerm zero) ‚à∑ [])
_ = refl
#+END_SRC

** Example: Simple Types
#+BEGIN_SRC org-agda
import Data.Vec as V
import Data.Fin as F

_ : quoteTerm ‚Ñï ‚â° def (quote ‚Ñï) []
_ = refl

_ : quoteTerm V.Vec ‚â° def (quote V.Vec) []
_ = refl

_ : quoteTerm (F.Fin 3) ‚â° def (quote F.Fin) (ùìãùìáùí∂ (lit (nat 3)) ‚à∑ [])
_ = refl
#+END_SRC

** Example: Lambda Terms

First we show how reductions with lambdas works then we show how lambda functions
are represented as ~Term~ values.

~quoteTerm~ typechecks and normalises its argument before yielding a ~Term~ value.
#+BEGIN_SRC org-agda
_ : quoteTerm ((Œª x ‚Üí x) "nice") ‚â° lit (string "nice")
_ = refl
#+END_SRC

Eta-reduction happens, ~f ‚âà Œª x ‚Üí f x~.
#+BEGIN_SRC org-agda
id : {A : Set} ‚Üí A ‚Üí A
id x = x

_ :   quoteTerm (Œª (x : ‚Ñï) ‚Üí id x)
    ‚â° def (quote id) (ùíΩùìáùí∂ (def (quote ‚Ñï) []) ‚à∑ [])
_ = refl
#+END_SRC

No delta-reduction happens; function definitions are not elaborated.
#+BEGIN_SRC org-agda
_ :   quoteTerm (id "a")
    ‚â° def (quote id)
        (ùíΩùìáùí∂ (def (quote String) []) ‚à∑  ùìãùìáùí∂ (lit (string "a")) ‚à∑ [])
_ = refl
#+END_SRC

Here is a simple identity function on the Booleans.
A ‚Äúlam‚Äùda with a ‚Äúvisible‚Äù ‚Äúabs‚Äùtract argument named ~"x"~ is introduced
having as body merely being the 0 nearest-bound variable, applied to an empty
list of arguments.
#+BEGIN_SRC org-agda
_ : quoteTerm (Œª (x : Bool) ‚Üí x) ‚â° lam visible (abs "x" (var 0 []))
_ = refl

#+END_SRC

Here is a more complicated lambda abstraction: Note that ~f a~ is represented as
the variable 0 lambdas away from the body applied to the variable 1 lambda away
from the body.
#+BEGIN_SRC org-agda
_ : quoteTerm (Œª (a : ‚Ñï) (f : ‚Ñï ‚Üí ‚Ñï) ‚Üí f a)
    ‚â°  lam visible (abs "a"
         (lam visible (abs "f"
           (var 0 (arg (arg-info visible relevant) (var 1 []) ‚à∑ [])))))
_ = refl
#+END_SRC

This is rather messy, let's introduce some syntactic sugar to make it more readable.
#+BEGIN_SRC org-agda
infixr 5 Œªùìã_‚Ü¶_  ŒªùíΩ_‚Ü¶_

Œªùìã_‚Ü¶_  ŒªùíΩ_‚Ü¶_ : String ‚Üí Term ‚Üí Term
Œªùìã x ‚Ü¶ body  = lam visible (abs x body)
ŒªùíΩ x ‚Ü¶ body  = lam hidden (abs x body)
#+END_SRC
Now the previous example is a bit easier on the eyes:
#+BEGIN_SRC org-agda
_ :   quoteTerm (Œª (a : ‚Ñï) (f : ‚Ñï ‚Üí ‚Ñï) ‚Üí f a)
    ‚â° Œªùìã "a" ‚Ü¶ Œªùìã "f" ‚Ü¶ var 0 [ ùìãùìáùí∂ (var 1 []) ]
_ = refl
#+END_SRC

Using that delicious sugar, let's look at the constant function a number of ways.
#+BEGIN_SRC org-agda
_ : {A B : Set} ‚Üí   quoteTerm (Œª (a : A) (b : B) ‚Üí a)
                  ‚â° Œªùìã "a" ‚Ü¶ (Œªùìã "b" ‚Ü¶ var 1 [])
_ = refl

_ :  quoteTerm (Œª {A B : Set} (a : A) (_ : B) ‚Üí a)
    ‚â° (ŒªùíΩ "A" ‚Ü¶ (ŒªùíΩ "B" ‚Ü¶ (Œªùìã "a" ‚Ü¶ (Œªùìã "_" ‚Ü¶ var 1 []))))
_ = refl

const : {A B : Set} ‚Üí A ‚Üí B ‚Üí A
const a _ = a

_ : quoteTerm const ‚â° def (quote const) []
_ = refl
#+END_SRC

* COMMENT Monad Setup

#+BEGIN_SRC org-agda
_>>=_        : ‚àÄ {a b} {A : Set a} {B : Set b} ‚Üí TC A ‚Üí (A ‚Üí TC B) ‚Üí TC B
_>>=_ = bindTC

_>>_        : ‚àÄ {a b} {A : Set a} {B : Set b} ‚Üí TC A ‚Üí TC B ‚Üí TC B
_>>_  = Œª p q ‚Üí p >>= (Œª _ ‚Üí q)
#+END_SRC
* COMMENT Two theorems from a proof of ~x + 2 ‚â° y~

Suppose we have the following theorem ~p~.
#+BEGIN_SRC org-agda
postulate
  x y : ‚Ñï
  p   : x + 2 ‚â° y
#+END_SRC

Let's make some helpful abbreviations.
#+BEGIN_SRC org-agda
ùìÅ‚ÇÄ = arg (arg-info hidden relevant) (def (quote Level.zero) [])
ùí© = arg (arg-info hidden relevant) (def (quote ‚Ñï) [])
#+END_SRC

* COMMENT A Spec environment
Here's a literate Agda ~spec~-ification environment, which corresponds to an Org-mode ~EXAMPLE~ block.
#+BEGIN_EXAMPLE org-agda
module this-is-a-spec {A : Set} (_‚â§_ : A ‚Üí A ‚Üí Set) where

  maximum-specfication : (candidate : A) ‚Üí Set
  maximum-specfication c = ?
#+END_EXAMPLE

* COMMENT README

C-c C-c: evalute src block

#+NAME: make-readme
#+BEGIN_SRC emacs-lisp :results none
(with-temp-buffer
    (insert
    "#+EXPORT_FILE_NAME: README.md
     # HTML: <h1> gentle-intro-to-reflection </h1>

     A slow-paced introduction to reflection in Agda. ---Tactics!

     # The following can also be read as a [[https://alhassy.github.io/literate/][blog post]].

     #+TOC: headlines 2
     #+INCLUDE: gentle-intro-to-reflection.lagda
    ")
    (org-mode)
    (org-md-export-to-markdown)
)
#+END_SRC

* COMMENT footer

(progn (org-babel-goto-named-src-block "make-readme") (org-babel-execute-src-block) (outline-hide-sublevels 1))

Repo: https://github.com/alhassy/org-agda-mode

# Local Variables:
# eval: (visual-line-mode t)
# eval: (load-file "~/org-agda-mode/org-agda-mode.el")
# eval: (load-file "~/org-agda-mode/literate.el")
# End:
